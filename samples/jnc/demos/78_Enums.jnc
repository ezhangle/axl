// jancy also brings a couple of enhancements for enumeration types.
// in most languages enumerations expose member constant identifers to the parent namespace,
// which i always thought was an inherently wrong design decision cause of namespace pollution.
// enums in jancy conseal all member identifiers within enum namespace.
// to simplify porting of existing C/C++ code jancy offers 'enumc' variation,
// which behaves like a traditional C/C++ enum (exposing member identifiers).
// yet another flavour of enums which i always wanted to have is flag enum, i.e. enum
// dedicated to describe set of flags. 'enumf' differs from normal 'enum' in 2 key aspects:
// - automatic value assignment goes 1, 2, 4, 8 etc (increments previous bit position)
// - bitwise or operator '|' on 2 operands of matching 'enumf' type yields the same 'enumf' type
// - it's ok to assign '0' to 'enumf'
// note that like normal 'enum', 'enumf' does not pollute parent namespace.

//.............................................................................

enum Color
{
	Red,      // = 0 
	Orange,
	Yellow,
	Green = 15, 
	Blue,
	Purple,
}

enumf OpenFlags
{
	ReadOnly,      // = 0x01
	Exclusive = 0x20,
	DeleteOnClose, // = 0x40
}

enumc State
{
	State_Idle, // = 0
	State_Connecting,
	State_Connected,
	State_Disconnecting,
}

//.............................................................................

// entry point

int 
main ()
{
	printf ("main ()\n");

	Color color = Color.Blue;
	// color = 0; // <-- error cast int->enum must be explicit
	printf ("color = %d\n", color);

	State state = State_Connecting; // State.State_Connecting works also
	// state = 100; // <-- error cast int->enum must be explicit
	printf ("state = %d\n", state);

	OpenFlags flags = 0; // 0 is ok to assign to 'enumf'
	 
	flags = OpenFlags.ReadOnly | OpenFlags.Exclusive | OpenFlags.DeleteOnClose;
	// flags = 200; // <-- error cast int->enum must be explicit
	printf ("flags = %x\n", flags);

	return 0;
}

//.............................................................................

// system functions

int
printf (
	unsafe thin const char* format,
	unsafe ...
	);

//.............................................................................
