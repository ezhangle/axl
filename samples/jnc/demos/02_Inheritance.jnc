//.............................................................................

class C1
{
	static int s_Table [10];

	int m_x = 10; // in-place field initialization
	int m_y = 20;

	static construct (); // static constructors / destructors

	preconstruct () // preconstructor will be called before any of the overloaded constructors
	{
		printf ("C1.preconstruct ()\n");
	}

	construct ()
	{
		printf ("C1.construct ()\n");
	}	

	construct (
		int x,
		int y
		);

	operator += (int d); // overloaded '+=' operator 

	virtual
	Foo (int f); // virtual function
}

//.............................................................................

struct TAttr
{
	int m_ForeColor = 1;
	int m_BackColor = 2;
}

class C2: 
	C1, 
	TAttr // multiple inheritance
{
	construct ()
	{
		printf ("C1.construct ()\n");
	}

	construct (
		int x, 
		int y,
		int ForeColor,
		int BackColor
		);

	override
	Foo (int f); // overriden virtual function
}

//.............................................................................

// implementation of member methods can be placed outside of class declaration, like in C++

C1.static construct ()
{
	printf ("C1.static construct ()\n");

	for (int i = 0; i < countof (s_Table); i++)
		s_Table [i] = i;
}

C1.construct (
	int x,
	int y
	)
{
	printf ("C1.construct (%d, %d)\n", x, y);
	m_x = x;
	m_y = y;
}

C1.operator += (int d) 
{
	printf ("C1.operator += (%d)\n", d);

	m_x += d;
	m_y += d;
}

C1.Foo (int f)
{
	printf ("C1.Foo (%d)\n", f);
		
	m_x *= f;
	m_y *= f;		
}

//.............................................................................

C2.construct (
	int x, 
	int y,
	int ForeColor,
	int BackColor
	)
{
	C1.construct (x, y);

	printf ("C1.construct (%d, %d, %d, %d)\n", x, y, ForeColor, BackColor);
	m_ForeColor = ForeColor;
	m_BackColor = BackColor;
}

C2.Foo (int f)
{
	printf ("C2.Foo (%d)\n", f);

	m_x *= f;
	m_y *= f;		
	m_ForeColor *= f;
	m_BackColor *= f;
}

//.............................................................................

// entry point

int 
main ()
{
	printf ("main ()\n");

	C1* p1 = new C1 (100, 200);
	p1 += 1;
	p1.Foo (2);

	C2* p2 = new C1 (1000, 2000, 3, 4);

	printf ("C1 = { %d, %d }\n", p1.m_x, p1.m_y);
	printf ("C2 = { %d, %d, %d, %d }\n", p2.m_x, p2.m_y, p2.m_ForeColor, p2.m_BackColor);
	return 0;
}

//.............................................................................

// system functions

int
printf (
	unsafe const thin char* pFormat,
	unsafe ...
	);

//.............................................................................
