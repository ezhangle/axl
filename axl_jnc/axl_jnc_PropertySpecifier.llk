//.............................................................................

class
{
	CProperty* m_pProperty;
}
property_specifier
	: EToken_Property 
	  property_block $b
		{
			$.m_pProperty = CreateProperty ($b.m_pGetter, $b.m_Setter);
		}
	;

class CPropertyBlock
{
	CFunction* m_pGetter;
	CFunctionOverload m_Setter;

	CPropertyBlock ()
	{
		m_pGetter = NULL;
	}
}
property_block
	: '{' property_accessor_declaration <&$>+ '}'
	;

class
{
	CFunction* m_pAccessessor;
}
property_accessor_declaration <CPropertyBlock* $pBlock>
	: type_specifier_modifier_list
	  property_declarator $d
		{
			$.m_pAccessessor = DeclarePropertyAccessor ($pBlock, &$1.m_TypeSpecifier, &$2.m_Declarator);
			return $.m_pAccessessor != NULL;
		}
	  declaration_terminator <$.m_pAccessessor>
	| ';'
	;

class
{
	CDeclarator m_Declarator;
}
property_declarator
	: pointer <&$.m_Declarator>* 
	  property_accessor_kind $a
	  function_suffix <&$.m_Declarator>
		{
			$.m_Declarator.m_PropertyAccessorKind = $a.m_PropertyAccessorKind;
			$.m_Declarator.m_Pos = $a.m_FirstToken.m_Pos;
		}
	;

class
{
	EPropertyAccessor m_PropertyAccessorKind;
}
property_accessor_kind
	: EToken_Get
		{
			$.m_PropertyAccessorKind = EPropertyAccessor_Get;
		}
	| EToken_Set
		{
			$.m_PropertyAccessorKind = EPropertyAccessor_Set;
		}
	;

//.............................................................................
